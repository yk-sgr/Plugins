plugins {
    id 'java'
    id 'maven'
    id 'signing'
}

group 'de.foryasee'
version '1.1.0'

sourceCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

def sonaUsername = hasProperty('sonatypeUsername') ? project.property('sonatypeUsername') : System.getenv('sonatypeUsername')
def sonaPassword = hasProperty('sonatypePassword') ? project.property('sonatypePassword') : System.getenv('sonatypePassword')

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: sonaUsername, password: sonaPassword)
            }

            pom.project {
                name 'Plugins'
                packaging 'jar'
                description 'Simple loading of plugins.'
                url 'https://github.com/ForYaSee/Plugins'

                scm {
                    url 'scm:git@github.com:ForYaSee/Plugins.git'
                    connection 'scm:git@github.com:ForYaSee/Plugins.git'
                    developerConnection 'scm:git@github.com:ForYaSee/Plugins.git'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'yannickseeger'
                        name 'Yannick Seeger'
                    }
                }
            }
        }
    }
}